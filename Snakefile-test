# Snakefile
import os

# 配置路径
OUTPUT_DIR = "/mnt/ramdisk/rna/jzp/output"
TEMP_DIR = config.get('temp_dir') or "/tmp"
CASE_ID = config.get('case_id') or "SRR23538290"

INPUT_DIR = "/mnt/treasure/asc25/rna/fastq"
REF_DIR = "/mnt/treasure/asc25/rna/input"
FASTQ_DIR = INPUT_DIR


CUTSEQ = "cutseq"
HISAT = "./hisat-3n/hisat-3n"
HISAT_TABLE = "./hisat-3n/hisat-3n-table"
SAMTOOLS = "./samtools/samtools"

NUM_PROC = 4
id_list = [i for i in range(NUM_PROC)]

# 最终目标
rule all:
    input:
        expand(f"{OUTPUT_DIR}/{CASE_ID}_map2genome.output.{{id}}.summary", id=id_list)

# 1. 修剪 FASTQ
rule cut_fastq:
    input:
        fastq = f"{FASTQ_DIR}/{CASE_ID}.fastq"
    output:
        cut = pipe(f"{OUTPUT_DIR}/{CASE_ID}.fastq_cut")
    shell:
        """
        {CUTSEQ} {input.fastq} -t 60 -A INLINE -m 20 --trim-polyA --ensure-inline-barcode \
        -o {output.cut} -O "{OUTPUT_DIR}/{CASE_ID}.fastq"
        """

# 2. split FASTQ_CUT
rule split_fastq:
    input:
        fastq_cut = f"{OUTPUT_DIR}/{CASE_ID}.fastq_cut"
    output:
        split_cut = expand(f"{OUTPUT_DIR}/{CASE_ID}.{{id}}.fastq_cut", id=id_list)
    shell:
        """
        ./split/split_fastq {OUTPUT_DIR}/{CASE_ID} {NUM_PROC} < {input.fastq_cut}
        """

# 3. 比对到 ncRNA
rule align_to_ncrna:
    input:
        fastq_cut = f"{OUTPUT_DIR}/{CASE_ID}.{{id}}.fastq_cut",
        ref_fa_ncrna = f"{REF_DIR}/Homo_sapiens.GRCh38.ncrna.fa",
    output:
        unmapped_bam = pipe(f"{OUTPUT_DIR}/{CASE_ID}.ncrna.unmapped.{{id}}.bam"),
        summary = f"{OUTPUT_DIR}/{CASE_ID}_map2ncrna.output.{{id}}.summary"
    shell:
        """
        {HISAT} --index {input.ref_fa_ncrna} --summary-file {output.summary} \
        --new-summary -q -U {input.fastq_cut} -p 16 --base-change C,T --mp 8,2 --no-spliced-alignment \
        --directional-mapping | \
        {SAMTOOLS} view -e 'flag.unmap' -u -o {output.unmapped_bam}
        """

# 4. 从 BAM 转 FASTQ
rule bam_to_fastq:
    input:
        unmapped_bam = f"{OUTPUT_DIR}/{CASE_ID}.ncrna.unmapped.{{id}}.bam"
    output:
        mRNA_fastq = temp(f"{OUTPUT_DIR}/{CASE_ID}.mRNA.{{id}}.fastq")
    shell:
        """
        {SAMTOOLS} fastq -O {input.unmapped_bam} > {output.mRNA_fastq}
        """

# 5. 比对到基因组
rule align_to_genome:
    input:
        fastq = f"{OUTPUT_DIR}/{CASE_ID}.mRNA.{{id}}.fastq"
    output:
        mapped_bam = f"{OUTPUT_DIR}/{CASE_ID}.mRNA.genome.mapped.{{id}}.bam",
        summary = f"{OUTPUT_DIR}/{CASE_ID}_map2genome.output.{{id}}.summary"
    shell:
        """
        {HISAT} --index {REF_DIR}/Homo_sapiens.GRCh38.dna.primary_assembly.fa -p 16 --summary-file {output.summary} \
        --new-summary -q -U {input.fastq} --directional-mapping --base-change C,T --pen-noncansplice 20 --mp 4,1 | \
        {SAMTOOLS} view -e '!flag.unmap' -u -o {output.mapped_bam}
        """
